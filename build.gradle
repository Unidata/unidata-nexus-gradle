plugins {
  id 'groovy'
  id 'java-gradle-plugin' // Adds gradleApi() compile dep and gradleTestKit() testCompile dep.
  id 'jacoco'
  id 'maven-publish'
}

repositories {
  jcenter()
}

group 'edu.ucar.unidata'
version '0.0.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

gradlePlugin {
  plugins {
    rawRepoPlugin {
      id = 'edu.ucar.unidata.nexus'
      displayName = 'Plugin for Publishing to the Unidata Nexus Server'
      description = 'A gradle plugin for managing Raw Repository publications on the Unidata Nexus repository'
      implementationClass = 'edu.ucar.build.publishing.NexusPlugin'
    }
  }
}

dependencies {
  implementation localGroovy()
  implementation 'io.github.http-builder-ng:http-builder-ng-okhttp:1.0.3'

  testImplementation 'org.slf4j:slf4j-api:1.7.28'
  testRuntimeOnly 'ch.qos.logback:logback-classic:1.2.3'

  // We need to make sure the version of spock is compatible with the version of groovy being used by the plugin
  // project. If the version of groovy changes (because we update gradle), we should make sure to find a spock
  // version that works.
  testImplementation('org.spockframework:spock-core:1.3-groovy-2.5') {
    // The Gradle API drags in the bundled version of Groovy that Gradle ships with (localGroovy()) â€“
    // see https://discuss.gradle.org/t/unable-to-force-gradle-to-use-groovy-2-0-0-for-the-project/7021.
    // Spock drags in another Groovy as a transitive dependency.
    //
    // So, there are multiple candidate versions of the Groovy dependency. Ordinarily, this is no problem for
    // Gradle; it has version-conflict-resolution machinery to deal with it. However, localGroovy() is a special
    // kind of dependency that doesn't participate in that resolution (again, see above URL).
    //
    // Once added to a configuration, localGroovy() will remain part of it no matter what. Furthermore, since
    // localGroovy() is "special", Gradle will not consider a second, "normal" Groovy dependency to be in conflict
    // with it. This can result in two different versions of Groovy in the same configuration, and ultimately
    // errors like: "groovy.lang.GroovyRuntimeException: Conflicting module versions. Module [groovy-all is loaded
    // in version 2.4.4 and you are trying to load version 2.4.1".
    //
    // Our solution is to explicitly exclude any other Groovy dependencies whenever localGroovy() is already part
    // of the configuration.
    exclude module: 'groovy-all'
  }
}

tasks.named('wrapper') {
  distributionType = Wrapper.DistributionType.ALL
  // when upgrading, make sure we are using a compatible version of spock-core for testing.
  gradleVersion = '6.7.1'
}

ext {
  NEXUS_USERNAME_KEY = 'nexus.username'
  NEXUS_PASSWORD_KEY = 'nexus.password'
}

String getPropertyOrFailBuild(String key) {
  if (!hasProperty(key)) {
    throw new GradleException("You must define the '$key' property.")
  } else {
    property(key) as String
  }
}

publishing {
  repositories {
    String version = rootProject.version as String
    if (version.endsWith('SNAPSHOT')) {
      maven {
        name = 'snapshots'
        url = 'https://artifacts.unidata.ucar.edu/repository/unidata-snapshots/'
      }
    } else {
      maven {
        name = 'releases'
        url = 'https://artifacts.unidata.ucar.edu/repository/unidata-releases/'
      }
    }
  }
}

// The "publish" tasks require credentials for our Nexus server, which they look for in Gradle properties.
// If those properties (i.e. NEXUS_USERNAME_KEY and NEXUS_PASSWORD_KEY) haven't been provided, the build will fail.
// Therefore, we only want to configure credentials when a "publish" task is part of the execution plan. Otherwise,
// unavailable credentials could cause a build to fail even if we aren't doing any publishing. The TaskExecutionGraph
// allows us to do that.
gradle.taskGraph.whenReady {TaskExecutionGraph taskGraph ->
  // This won't find any publishToMavenLocal tasks. Those are of type PublishToMavenLocal
  Collection<Task> mavenPublishTasks = taskGraph.allTasks.findAll {
    it instanceof PublishToMavenRepository
  }

  mavenPublishTasks.each {
    it.repository.credentials.with {
      username = getPropertyOrFailBuild NEXUS_USERNAME_KEY
      password = getPropertyOrFailBuild NEXUS_PASSWORD_KEY
    }
  }
}
